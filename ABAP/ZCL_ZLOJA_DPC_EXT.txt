class ZCL_ZLOJA_DPC_EXT definition
  public
  inheriting from ZCL_ZLOJA_DPC
  create public .

public section.
protected section.

  methods CLIENTESET_GET_ENTITY
    redefinition .
  methods CLIENTESET_GET_ENTITYSET
    redefinition .
  methods VENDASET_GET_ENTITYSET
    redefinition .
  methods CLIENTESET_UPDATE_ENTITY
    redefinition .
  PRIVATE SECTION.
ENDCLASS.



CLASS ZCL_ZLOJA_DPC_EXT IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Protected Method ZCL_ZLOJA_DPC_EXT->CLIENTESET_GET_ENTITY
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_ENTITY_NAME                 TYPE        STRING
* | [--->] IV_ENTITY_SET_NAME             TYPE        STRING
* | [--->] IV_SOURCE_NAME                 TYPE        STRING
* | [--->] IT_KEY_TAB                     TYPE        /IWBEP/T_MGW_NAME_VALUE_PAIR
* | [--->] IO_REQUEST_OBJECT              TYPE REF TO /IWBEP/IF_MGW_REQ_ENTITY(optional)
* | [--->] IO_TECH_REQUEST_CONTEXT        TYPE REF TO /IWBEP/IF_MGW_REQ_ENTITY(optional)
* | [--->] IT_NAVIGATION_PATH             TYPE        /IWBEP/T_MGW_NAVIGATION_PATH
* | [<---] ER_ENTITY                      TYPE        ZCL_ZLOJA_MPC=>TS_CLIENTE
* | [<---] ES_RESPONSE_CONTEXT            TYPE        /IWBEP/IF_MGW_APPL_SRV_RUNTIME=>TY_S_MGW_RESPONSE_ENTITY_CNTXT
* | [!CX!] /IWBEP/CX_MGW_BUSI_EXCEPTION
* | [!CX!] /IWBEP/CX_MGW_TECH_EXCEPTION
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD CLIENTESET_GET_ENTITY.

    DATA: LV_CLIENTE TYPE ZTAB_VENDAS_CAB-CLIENTE_ID.

    CASE IV_SOURCE_NAME.

      WHEN 'Venda'.

        READ TABLE IT_NAVIGATION_PATH
        WITH KEY NAV_PROP = 'cliente'
        TRANSPORTING NO FIELDS.

        IF SY-SUBRC EQ 0.

          READ TABLE IT_KEY_TAB
          WITH KEY NAME = 'IDVenda'
          REFERENCE INTO DATA(LV_REF_KEY).

          SELECT SINGLE CLIENTE_ID INTO LV_CLIENTE
            FROM ZTAB_VENDAS_CAB
            WHERE ID EQ LV_REF_KEY->VALUE.

        ENDIF.

      WHEN OTHERS.

        READ TABLE IT_KEY_TAB
          WITH KEY NAME = 'ID'
          REFERENCE INTO LV_REF_KEY.

        LV_CLIENTE = LV_REF_KEY->VALUE.

    ENDCASE.


    SELECT SINGLE *
    FROM ZTAB_CLIENTES
    INTO CORRESPONDING FIELDS OF ER_ENTITY
    WHERE ID EQ LV_CLIENTE.

  ENDMETHOD.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Protected Method ZCL_ZLOJA_DPC_EXT->CLIENTESET_GET_ENTITYSET
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_ENTITY_NAME                 TYPE        STRING
* | [--->] IV_ENTITY_SET_NAME             TYPE        STRING
* | [--->] IV_SOURCE_NAME                 TYPE        STRING
* | [--->] IT_FILTER_SELECT_OPTIONS       TYPE        /IWBEP/T_MGW_SELECT_OPTION
* | [--->] IS_PAGING                      TYPE        /IWBEP/S_MGW_PAGING
* | [--->] IT_KEY_TAB                     TYPE        /IWBEP/T_MGW_NAME_VALUE_PAIR
* | [--->] IT_NAVIGATION_PATH             TYPE        /IWBEP/T_MGW_NAVIGATION_PATH
* | [--->] IT_ORDER                       TYPE        /IWBEP/T_MGW_SORTING_ORDER
* | [--->] IV_FILTER_STRING               TYPE        STRING
* | [--->] IV_SEARCH_STRING               TYPE        STRING
* | [--->] IO_TECH_REQUEST_CONTEXT        TYPE REF TO /IWBEP/IF_MGW_REQ_ENTITYSET(optional)
* | [<---] ET_ENTITYSET                   TYPE        ZCL_ZLOJA_MPC=>TT_CLIENTE
* | [<---] ES_RESPONSE_CONTEXT            TYPE        /IWBEP/IF_MGW_APPL_SRV_RUNTIME=>TY_S_MGW_RESPONSE_CONTEXT
* | [!CX!] /IWBEP/CX_MGW_BUSI_EXCEPTION
* | [!CX!] /IWBEP/CX_MGW_TECH_EXCEPTION
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD CLIENTESET_GET_ENTITYSET.

    DATA:
            LR_NOME TYPE RANGE OF ZTAB_CLIENTES-NOME.

    "Montagem do Range de Filtros
    LOOP AT IT_FILTER_SELECT_OPTIONS REFERENCE INTO DATA(LV_REF_FILTER). "tabela contendo os campos de tela (parametros) e respectiva lista de valores

      CASE LV_REF_FILTER->PROPERTY.

        WHEN 'Nome'.

          LOOP AT LV_REF_FILTER->SELECT_OPTIONS REFERENCE INTO DATA(LV_REF_SELECT_OPTIONS). "loop na tabela de valores do parametro
            APPEND INITIAL LINE TO LR_NOME REFERENCE INTO DATA(LV_REF_R_NOME).
            MOVE-CORRESPONDING LV_REF_SELECT_OPTIONS->* TO LV_REF_R_NOME->*.
          ENDLOOP.

      ENDCASE.

    ENDLOOP.

    "ordenamento
    LOOP AT IT_ORDER REFERENCE INTO DATA(LV_REF_ORDER)."tabela de campos a serem ordenados

      CASE LV_REF_ORDER->PROPERTY.
        WHEN 'Nome'.
          DATA(LV_DB_FIELD) = 'NOME'.
      ENDCASE.

      DATA(LV_ORDER) = LV_REF_ORDER->ORDER.

      TRANSLATE LV_ORDER TO UPPER CASE.

      CASE LV_ORDER.
        WHEN 'DESC'.
          DATA(LV_DB_ORDER) = 'DESCENDING'.
        WHEN OTHERS.
          LV_DB_ORDER = 'ASCENDING'.
      ENDCASE.

      CONCATENATE
        LV_DB_FIELD
        LV_DB_ORDER
      INTO
        DATA(LV_ORDER_BY)
      SEPARATED BY
        SPACE.

    ENDLOOP.

  IF IO_TECH_REQUEST_CONTEXT->HAS_COUNT( ) = ABAP_TRUE.

    SELECT COUNT(*)
    FROM ZTAB_CLIENTES
    INTO @DATA(LV_COUNT)
    WHERE NOME IN @LR_NOME.

    ES_RESPONSE_CONTEXT-COUNT = LV_COUNT.

  ELSE.

    DATA(LV_UP_TO) = IS_PAGING-TOP + IS_PAGING-SKIP.

     SELECT *
     FROM ZTAB_CLIENTES
     UP TO LV_UP_TO ROWS

     INTO CORRESPONDING FIELDS OF TABLE ET_ENTITYSET
     WHERE NOME IN LR_NOME
     ORDER BY (LV_ORDER_BY).

    IF IS_PAGING-SKIP > 0.
      DELETE ET_ENTITYSET FROM 1 TO IS_PAGING-SKIP.
    ENDIF.

  ENDIF.

  ENDMETHOD.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Protected Method ZCL_ZLOJA_DPC_EXT->CLIENTESET_UPDATE_ENTITY
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_ENTITY_NAME                 TYPE        STRING
* | [--->] IV_ENTITY_SET_NAME             TYPE        STRING
* | [--->] IV_SOURCE_NAME                 TYPE        STRING
* | [--->] IT_KEY_TAB                     TYPE        /IWBEP/T_MGW_NAME_VALUE_PAIR
* | [--->] IO_TECH_REQUEST_CONTEXT        TYPE REF TO /IWBEP/IF_MGW_REQ_ENTITY_U(optional)
* | [--->] IT_NAVIGATION_PATH             TYPE        /IWBEP/T_MGW_NAVIGATION_PATH
* | [--->] IO_DATA_PROVIDER               TYPE REF TO /IWBEP/IF_MGW_ENTRY_PROVIDER(optional)
* | [<---] ER_ENTITY                      TYPE        ZCL_ZLOJA_MPC=>TS_CLIENTE
* | [!CX!] /IWBEP/CX_MGW_BUSI_EXCEPTION
* | [!CX!] /IWBEP/CX_MGW_TECH_EXCEPTION
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD CLIENTESET_UPDATE_ENTITY.

    IO_DATA_PROVIDER->READ_ENTRY_DATA(
      IMPORTING
        ES_DATA = ER_ENTITY ).

    DATA:
          LS_CLIENTE TYPE ZTAB_CLIENTES.

    MOVE-CORRESPONDING ER_ENTITY TO LS_CLIENTE.

    UPDATE ZTAB_CLIENTES FROM LS_CLIENTE.

  ENDMETHOD.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Protected Method ZCL_ZLOJA_DPC_EXT->VENDASET_GET_ENTITYSET
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_ENTITY_NAME                 TYPE        STRING
* | [--->] IV_ENTITY_SET_NAME             TYPE        STRING
* | [--->] IV_SOURCE_NAME                 TYPE        STRING
* | [--->] IT_FILTER_SELECT_OPTIONS       TYPE        /IWBEP/T_MGW_SELECT_OPTION
* | [--->] IS_PAGING                      TYPE        /IWBEP/S_MGW_PAGING
* | [--->] IT_KEY_TAB                     TYPE        /IWBEP/T_MGW_NAME_VALUE_PAIR
* | [--->] IT_NAVIGATION_PATH             TYPE        /IWBEP/T_MGW_NAVIGATION_PATH
* | [--->] IT_ORDER                       TYPE        /IWBEP/T_MGW_SORTING_ORDER
* | [--->] IV_FILTER_STRING               TYPE        STRING
* | [--->] IV_SEARCH_STRING               TYPE        STRING
* | [--->] IO_TECH_REQUEST_CONTEXT        TYPE REF TO /IWBEP/IF_MGW_REQ_ENTITYSET(optional)
* | [<---] ET_ENTITYSET                   TYPE        ZCL_ZLOJA_MPC=>TT_VENDA
* | [<---] ES_RESPONSE_CONTEXT            TYPE        /IWBEP/IF_MGW_APPL_SRV_RUNTIME=>TY_S_MGW_RESPONSE_CONTEXT
* | [!CX!] /IWBEP/CX_MGW_BUSI_EXCEPTION
* | [!CX!] /IWBEP/CX_MGW_TECH_EXCEPTION
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD VENDASET_GET_ENTITYSET.

    DATA:
      LR_NOME    TYPE RANGE OF ZTAB_CLIENTES-NOME,
      LR_CLIENTE TYPE RANGE OF ZTAB_VENDAS_CAB-CLIENTE_ID.

    CASE IV_SOURCE_NAME.

      WHEN 'Cliente'.

        READ TABLE IT_NAVIGATION_PATH
        WITH KEY NAV_PROP = 'vendas'
        TRANSPORTING NO FIELDS.


        IF SY-SUBRC EQ 0.

          LOOP AT IT_KEY_TAB REFERENCE INTO DATA(LV_REF_KEY)
              WHERE NAME EQ 'ID'.

            APPEND INITIAL LINE TO LR_CLIENTE REFERENCE INTO DATA(LV_REF_R_CLIENTE).
            LV_REF_R_CLIENTE->SIGN = 'I'.
            LV_REF_R_CLIENTE->OPTION = 'EQ'.
            LV_REF_R_CLIENTE->LOW = LV_REF_KEY->VALUE.

          ENDLOOP.

        ENDIF.

    ENDCASE.

    "Montagem do Range de Filtros
    LOOP AT IT_FILTER_SELECT_OPTIONS REFERENCE INTO DATA(LV_REF_FILTER). "tabela contendo os campos de tela (parametros) e respectiva lista de valores

      CASE LV_REF_FILTER->PROPERTY.

        WHEN 'Descricao'.

          LOOP AT LV_REF_FILTER->SELECT_OPTIONS REFERENCE INTO DATA(LV_REF_SELECT_OPTIONS). "loop na tabela de valores do parametro
            APPEND INITIAL LINE TO LR_NOME REFERENCE INTO DATA(LV_REF_R_NOME).
            MOVE-CORRESPONDING LV_REF_SELECT_OPTIONS->* TO LV_REF_R_NOME->*.
          ENDLOOP.

      ENDCASE.

    ENDLOOP.

    "ordenamento
    LOOP AT IT_ORDER REFERENCE INTO DATA(LV_REF_ORDER)."tabela de campos a serem ordenados

      CASE LV_REF_ORDER->PROPERTY.
        WHEN 'DataCriacao'.
          DATA(LV_DB_FIELD) = 'DATA_CRIACAO'.
      ENDCASE.

      DATA(LV_ORDER) = LV_REF_ORDER->ORDER.

      TRANSLATE LV_ORDER TO UPPER CASE.

      CASE LV_ORDER.
        WHEN 'DESC'.
          DATA(LV_DB_ORDER) = 'DESCENDING'.
        WHEN OTHERS.
          LV_DB_ORDER = 'ASCENDING'.
      ENDCASE.

      CONCATENATE
        LV_DB_FIELD
        LV_DB_ORDER
      INTO
        DATA(LV_ORDER_BY)
      SEPARATED BY
        SPACE.

    ENDLOOP.

    IF IO_TECH_REQUEST_CONTEXT->HAS_COUNT( ) = ABAP_TRUE.

      SELECT COUNT(*)
      FROM ZTAB_VENDAS_CAB
      INTO @DATA(LV_COUNT)
      WHERE DESCRICAO IN @LR_NOME AND
        CLIENTE_ID IN @LR_CLIENTE.

      ES_RESPONSE_CONTEXT-COUNT = LV_COUNT.

    ELSE.

      DATA(LV_UP_TO) = IS_PAGING-TOP + IS_PAGING-SKIP.

      SELECT *
      FROM ZTAB_VENDAS_CAB
      UP TO LV_UP_TO ROWS

      INTO CORRESPONDING FIELDS OF TABLE ET_ENTITYSET
      WHERE DESCRICAO IN LR_NOME AND
      CLIENTE_ID IN LR_CLIENTE

      ORDER BY (LV_ORDER_BY).

      IF IS_PAGING-SKIP > 0.
        DELETE ET_ENTITYSET FROM 1 TO IS_PAGING-SKIP.
      ENDIF.

    ENDIF.


  ENDMETHOD.
ENDCLASS.